Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Overpowered buffs (without concentration) which have to be removed if its caster left the party OR changed spec
DB_DT_OPBuffs_List("LONGSTRIDER");
DB_DT_OPBuffs_List("WARDING_BOND");  // TODO: fix WARDING_BOND_CASTER left on caster
DB_DT_OPBuffs_List("PROTECTION_FROM_POISON");
DB_DT_OPBuffs_List("FREEDOM_OF_MOVEMENT");
DB_DT_OPBuffs_List("DEATH_WARD");
DB_DT_OPBuffs_List("HEROES_FEAST");
DB_DT_OPBuffs_List("AID");
DB_DT_OPBuffs_List("AID_3");
DB_DT_OPBuffs_List("AID_4");
DB_DT_OPBuffs_List("AID_5");
DB_DT_OPBuffs_List("AID_6");
DB_DT_OPBuffs_List("DARKVISION");
DB_DT_OPBuffs_List("MAGE_ARMOR");
DB_DT_OPBuffs_List("BARDIC_INSPIRATION");
DB_DT_OPBuffs_List("BARDIC_INSPIRATION_D8");
DB_DT_OPBuffs_List("BARDIC_INSPIRATION_D10");
DB_DT_OPBuffs_List("BARDIC_INSPIRATION_COMBAT");
DB_DT_OPBuffs_List("BARDIC_INSPIRATION_COMBAT_D8");
DB_DT_OPBuffs_List("BARDIC_INSPIRATION_COMBAT_D10");
DB_DT_OPBuffs_List("BARKSKIN");
//END_REGION
KBSECTION
//REGION OP Cast event handler
// Store casted spells info to check if some OP spell is casted on teammate
// data to store: TARGET, STATUS, CASTER
// override it every new status application (buffs replace each other not stack)

// Status is re-applied and we have a record about this cast type remove information about old caster
IF
StatusApplied((CHARACTER)_Target,_Status,(CHARACTER)_Caster,_)
AND
DB_DT_OPBuffs_Casted((CHARACTER)_Target,(STRING)_Status,(CHARACTER)_OldCaster)
THEN
NOT DB_DT_OPBuffs_Casted((CHARACTER)_Target,(STRING)_Status,(CHARACTER)_OldCaster);

// If status applied within teammates and cast is in DB_DT_OPBuffs_List - store info about it
IF
StatusApplied((CHARACTER)_Target,_Status,(CHARACTER)_Caster,_)
AND
DB_PartOfTheTeam(_Caster)
AND
DB_PartOfTheTeam(_Target)
AND
DB_DT_OPBuffs_List(_Status)
THEN
DB_DT_OPBuffs_Casted((CHARACTER)_Target,(STRING)_Status,(CHARACTER)_Caster);
//END_REGION


//REGION Party buffs handler
// Caster in party, target leaves
IF
DB_DT_OPBuffs_Casted((CHARACTER)_Target,(STRING)_Status,(CHARACTER)_Caster)
AND
_Target != _Caster  // allow to keep buff on self
AND
DB_PartyMembers(_Caster)
AND
NOT DB_PartyMembers(_Target)
AND
HasAppliedStatus(_Target, _Status, 1)
AND
Concatenate("Removing ", _Status, _msg1)
THEN
RemoveStatus(_Target, _Status, DT_OPBuffRemoving_4af7efc8-ab80-4ac3-bbe6-ce448f36f5f6);


// Target in party, caster leaves
IF
DB_DT_OPBuffs_Casted((CHARACTER)_Target,(STRING)_Status,(CHARACTER)_Caster)
AND
_Target != _Caster  // allow to keep buff on self
AND
NOT DB_PartyMembers(_Caster)
AND
DB_PartyMembers(_Target)
AND
HasAppliedStatus(_Target, _Status, 1)
THEN
RemoveStatus(_Target, _Status, DT_OPBuffRemoving_4af7efc8-ab80-4ac3-bbe6-ce448f36f5f6);
//END_REGION


//REGION Respec buffs bugfix  
// https://github.com/slafniy/DedTuned/issues/13
// Original game bug/feature but is has to be fixed
IF
RespecCompleted(_Player)
AND
DB_DT_OPBuffs_Casted((CHARACTER)_Target,(STRING)_Status,(CHARACTER)_Player)
AND
HasAppliedStatus(_Target, _Status, 1)
THEN
RemoveStatus(_Target, _Status, DT_OPBuffRemovingRespec_4af7efc8-ab80-4ac3-bbe6-ce448f36f5f6);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__Start"
